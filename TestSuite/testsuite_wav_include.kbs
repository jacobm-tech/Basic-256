# testsuite_wav_include section for BASIC256

# Modification History
# date		programmer	description
# 20140415	j.m.reneau	original coding

# WAV file for a musical scales were downloaded from
# https://ccrma.stanford.edu/~mromaine/220a/fp/sound-examples.html
# on 2014-04-14.

file$ = "eqt-major-sc.wav"
notes = 8
len = 6
#file$ = "eqt-chromo-sc.wav"
#notes = 13
#len = 9

if ostype = 2 then
   # android does not have enhansed WAV support
   # (WAVPLAY, WAVSTOP, and WAVWAIT only)
   #
   # simple play then stop
   wavplay file$
   pause 2 * len/notes
   wavstop
   call q("wavstop - did the first two notes of the scale play and then stop?")
   #
   # simple play to the end of the wav file
   wavplay file$
   wavwait
   call q("wavwait - did the entire 8 note scale play?")
else
   # other systems use Multimedia that allows for pause, seek, position and duration
   #
   # simple play then stop
   wavplay file$
   pause 2* len/notes
   wavstop
   call q("wavstop - did the first two notes of the scale play and then stop?")
   #
   call n("wavstop - check position 0",wavpos, 0)
   #
   wavplay # play from begining
   pause len/notes
   wavpause
   pause len/notes
   wavplay
   pause len/notes
   wavpause
   call q("wavstop - did the first two notes of the scale play with a rest between them and then stop?")
   #
   notel = wavlength() / notes
   call npercent("wavlength - check average note length",notel, len/notes, .10)
   #
   call npercent("wavpos - check position 2 notes",wavpos, 2*notel, .20)
   #
   # simple reload and play to the end of the wav file
   # play scale forward
   timer = msec
   wavseek 0
   wavplay
   wavwait
   timer = (msec - timer) / 1000 / wavlength()
   call npercent("wavwait - timer 100%",timer, 1, .10)
   call q("wavwait - did the scale play from low to high?")
   #
   # play scale backwards
   # sound currently at stopped state
   # should be in stop state from wavwait
   timer = msec
   wavplay file$
   for a = notes-1 to 0 step -1
      wavseek a * notel
      pause notel
   next a
   wavstop
   timer = (msec - timer) / 1000 / wavlength()
   call npercent("wavseek/wavstop - timer 100%",timer,1,.1)
   call q("wavseek/wavstop - did the scale play from high to low?")
   
endif

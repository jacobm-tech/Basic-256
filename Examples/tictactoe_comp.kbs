# tictactoe_comp
# with a computer player - 2009-12-23 - j.m.reneau
# requires BACIC256 0.9.4g or better

# -1=empty 0=x, 1=0
fastgraphics
dim board(9)
board = {-1,-1,-1,-1,-1,-1,-1,-1,-1}

gosub drawboard

print "tictactoe"
dim playertype$(2)
for t = 0 to 1
  playertype$[t] = ""
  while playertype$[t] <> "c" and playertype$[t] <> "h"
    print "who is player " + (t+1) + " (c-computer, h-human)";
    input playertype$[t]
  endwhile
next t

player = 1
otherplayer = 0
playerloop:
  # swap player and otherplayer
  t = player
  player = otherplayer
  otherplayer = t
  # play the player
  if playertype$[player] = "h" then gosub humanplayer
  if playertype$[player] = "c" then gosub computerplayer
  #  
  gosub drawboard
  # see if there is a winner and if not go to next player and wait
  gosub iswinner
  if winner = -1 then goto playerloop

  # we must have a winner
  print "The winner was ";
  if winner = 0 then print "X"
  if winner = 1 then print "O"
  if winner = 2 then print "cat"

  end

humanplayer:
  print "player ";
  if player = 0 then print "X";
  if player = 1 then print "O";
  print " please click on cell."

  clickclear

  while clickb = 0
    pause .1
  endwhile

  # what cell did they click on
  i = int(clickx/100) + int(clicky/100)*3
  # if it is not empty then wait again
  if board[i] <> -1 then goto humanplayer
  # set cell and display
  board[i] = player

  return

computerplayer:
  # go through each square and if it is a win do it
  for i = 0 to 8
      if board[i] = -1 then
        board[i] = player
        gosub iswinner
        if winner = player then return
        board[i] = -1
      endif
  next i
  #
  # go through each square and if it is a win for the other guy - block it
  for i = 0 to 8
      if board[i] = -1 then
        board[i] = otherplayer
        gosub iswinner
        if winner = otherplayer then
          board[i] = player
          return
        endif
        board[i] = -1
      endif
  next i
  # last resort make a random move
  i = int(rand * 9)
  while board[i] <> -1
    # try another
    i = int(rand * 9)
  endwhile
  board[i] = player
  return

iswinner: #
  # is there a winner = return winner - -1 if ther game continues
  # winner = 0 for X, winner=1 for Y, winner=2 for cat
  for winner = 0 to 1
    # check columns
    for t = 0 to 2
      if board[0+t] = winner and board[3+t] = winner and board[6+t] = winner then return
    next t
    # check rows
    for t = 0 to 2
      if board[3*t] = winner and board[3*t+1] = winner and board[3*t+2] = winner then return
    next t
    # check diagonals
    if board[0] = winner and board[4] = winner and board[8] = winner then return
    if board[2] = winner and board[4] = winner and board[6] = winner then return
  next winner
  # check for empty square 
  winner = -1
  for t = 0 to 8
    if board[t]=-1 then return
  next t
  # must be a cat
  winner = 2
  return

drawboard: #
  clg
  color black
  rect 0,95,300,10
  rect 0,195,300,10
  rect 95,0,10,300
  rect 195,0,10,300
  #
  font "Tahoma",50,100
  # draw X and O
  for t = 0 to 8
    if board[t]=0 then color red : text ((t % 3)*100)+25, (int(t/3)*100)+10, "X" 
    if board[t]=1 then color blue : text ((t % 3)*100)+25, (int(t/3)*100)+10, "O" 
  next t
  refresh
  return
